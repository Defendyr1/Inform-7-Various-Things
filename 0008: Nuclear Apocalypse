"Unknown Title" by neophyte.
The story headline is "A nuclear nightmare".

Include Basic Screen Effects by Emily Short. [status bar, wait for any input, clear the screen]
Use verbose room descriptions.

Test me with "jump / look / z"
[tab character: "	"]

[
Table of Contents
    Priorities Notes				Need to do stuff
    Gameplay Rules				Rules for so and so
    The when play begins rule			When play begins ...
    Player Character				Player Character informatio
    Rooms and Scenery				Each room / scenery
    Actions					New Actions
    Items					In-game items
    Unexcusable Cheats				Unfair cheat commands
    Tables of Reference Information		tables for accessing information
    Notes					comments on possible additions
    Work Area					dump code, refine, then sort
]

Section - Priority Notes -----------------------------------------------------------------

[Implement combat system]
[Implement XP leveling system]
[Implement wandering enemies via recurring scenes]
[Implement more rooms, items]
[Implement money / trade system]

Section - Gameplay Rules -----------------------------------------------------------------

[to stop blurting out hidden contents]
Rule for printing room description details of a closed container: 
	stop.

Rule for printing the name of a closed container (called C) while taking inventory:
	say "[printed name of C]";
	omit contents in listing.

[to take and auto-examine the item]
Report taking something:
	say "Taken. [run paragraph on]";
	try examining the noun instead.
	
[to be able to read items]
A thing has some text called the reading-material.
The reading-material of a thing is usually "".

Understand the command "read" as something new.
Reading is an action applying to one thing and requiring light.
Understand "read [something]" as reading.

Check reading:
	if the reading-material of the noun is "":
		try examining the noun instead.
		
Carry out reading:
	say "[reading-material of the noun]".
	
[a definition for on/off types of lights]
An electric light is a kind of device. 
Carry out switching on an electric light: 
	now the noun is lit.
 
Carry out switching off an electric light: 
	now the noun is unlit.

The examine devices rule is not listed in the carry out examining rules.

[will make the status bar]
Rule for constructing the status line:
	fill status bar with Table of Status Bar;
	rule succeeds.

Section - THE when play begins rule -------------------------------------------------------

When play begins:
	say "Starting the game...";
	now the strength of the player is (a random number from 1 to 6) + (a random number from 1 to 6) + (a random number from 1 to 6); 
	now the dexterity of the player is (a random number from 1 to 6) + (a random number from 1 to 6) + (a random number from 1 to 6);
	now the stamina of the player is (a random number from 1 to 6) + (a random number from 1 to 6) + (a random number from 1 to 6);
	now the charisma of the player is (a random number from 1 to 6) + (a random number from 1 to 6) + (a random number from 1 to 6);
	now the intelligence of the player is (a random number from 1 to 6) + (a random number from 1 to 6) + (a random number from 1 to 6);
	now the perception of the player is (a random number from 1 to 6) + (a random number from 1 to 6) + (a random number from 1 to 6);
	now the MaxHP of the player is 10;
	now the HP of the player is 10;
	now the defense of the player is 0;
	now the radiation of the player is 0;
	now the money of the player is 0;
	say "Would you like to be male (Y) or female (N)?";
	if the player consents:
		now the player is male;
		let x be a random number from 1 to 4;
		if x is 1: now the name of the player is "Alex";
		if x is 2: now the name of the player is "Aaron";
		if x is 3: now the name of the player is "Max";
		if x is 4: now the name of the player is "John";
	otherwise:
		now the player is female;
		let x be a random number from 1 to 4;
		if x is 1: now the name of the player is "Alexis";
		if x is 2: now the name of the player is "Erin";
		if x is 3: now the name of the player is "Max";
		if x is 4: now the name of the player is "Jane";
	say "Your name is [name of the player]. If you didn't like your name, too bad. Push a key to continue.";
	wait for any key;
	clear the screen; 
	say "[paragraph break]        You weren't a newspaper type of person, but when the first bomb exploded in New York, you made sure to read them. A terrorist had rigged some type of bomb. The UN responded with embargoes, threats, outright war. And the enemy sent more. More nukes. The UN had no other choice. No choice other than the Cold War era idea of Mutually Assured Destruction. They were MAD, so the bombs continued. After the first day, a cruel sense of revenge. After the second, doubt. Doubt that they had made the right decision After the third, nothing remained. Nothing but a ruined world, rampant extinction and fear. ";
	wait for any key;
	say "[paragraph break]        Fortunately, you had managed to flee in time to a small, rural town. No fallout never made it there, and some old doomsday prepper had a basement bunker. There were many corpses in there, for he and his family must have been the only survivors, until their suicide pact. You buried them all. Now the crows are circling as society rebuilds itself. Some will be violent, some will be peaceful. But what conquers all is fear of their own death.";
	wait for any key.

Section - Player Character-----------------------------------------------------------------

The carrying capacity of the player is 5.
The description of the player is "You don't have a mirror, but you're a [if the player is male]handsome devil[end if][if the player is female]beautiful lady[end if] nonetheless.".

A person has a number called HP.
A person has a number called MaxHP.
A person has a number called defense.
A person has a number called radiation.
A person has a number called strength.
A person has a number called dexterity.
A person has a number called stamina.
A person has a number called charisma.
A person has a number called intelligence.
A person has a number called perception.
A person has a number called money.
A person has some text called name.

Section - Rooms and Scenery ---------------------------------------------------------------

Homemade Bunker is a room.
The description of Homemade Bunker is "A rather coarse, homemade bunker. Various necessities line the walls - bed, water purifier, storage unit and the like. Going up leads to the ruins of the house. South leads to a garden, and west to a room filled with old technology.".

Bed is scenery in Homemade Bunker.
The description of Bed is "A ridiculously soft, comfy bed. Makes you wish you had slept through the apocalypse, or forever.".
	
water purifier is scenery in Homemade Bunker.
Understand "purifier" and "w purifier" and "water p" as the water purifier.
The description of water purifier is "It's a strange, horizontal tube lying upon three vertical tubes. Somehow, it creates clean water for you to drink. All you have to do is [bold type]get water[roman type], although, it does run out.".

storage is scenery in Homemade Bunker.
storage is fixed in place. [may need to remove]
storage is a closed container.
storage is a player's holdall.
Understand "storage unit" and "unit" as storage. 
The description of storage is "A storage unit. Contains a lot of things, and is rather nice to have.".

Control Room is a room.
Control Room is west of Homemade Bunker.
The description of Control Room is "A shabby concrete and iron structure. Around the old technology lies a Faraday cage, protecting it from EMPs. The technology is still functional, powered by what appears to be a stolen RTG nuclear battery. The rest of the bunker lies to the east.".

Faraday cage is scenery in Control Room. 
Understand "cage" and "faraday" and "f cage" as the faraday cage.
The description of the faraday cage is "It looks like chicken coop wire strung about all the electronics. It'll protect against EMPs, so you shouldn't mess with it.".

nuclearBattery is scenery in Control Room.
Understand "RTG" and "nuclear battery" as nuclearBattery.
The description of nuclearBattery is "A dangerous battery which gets energy from decaying radioactive materials. It also emits a fair amount of heat. You should definitely not mess with it, unless you like cancer.".

Control Panels is scenery in Control Room.
understand "old tech" and "old technology" and "control p" and "panels" as control panels.
The description of control panels is "A cobbled together piece of computer equipment. It seems that it controls defenses. Apparenetly the lights, garden center and water purification are all automated already. To use the panel, type [bold type]activate control panels[roman type].".

Garden Center is a room.
Garden Center is south of Homemade Bunker.
The description of Garden Center is "Rows of plants grow under flourescent lights, presumably powered from the control room. The watering system is all automatic, and there seems to be plenty of variety for food. You can [bold type]get food[roman type], although it is not infinite. The rest of the bunker lies to the north.".

Wrecked Bedroom is a room.
Wrecked Bedroom is above Homemade Bunker.
The description of Wrecked Bedroom is "A master bedroom, reasonably furnished. A trapdoor under a rug leads down to the bunker. To the east leads the rest of the house.".

rug is scenery in Wrecked Bedroom.
The description of rug is "It's really ugly. It's so nasty, nobody wants to move it, so your bunker is hidden.".

Section - Actions -------------------------------------------------------------------------

[reading command - see Section Gameplay Rules]

[for sleeping in Homemade Bunker's bed]
Understand the command "sleep" as something new. 
SleepingN is an action applying to nothing.
Understand "sleep" and "sleep in bed" as sleepingN.
Report sleepingN:
	if the player is in Homemade Bunker:
		say "You decide to take a quick nap. zzz.";
		now the HP of the player is the MaxHP of the player;
	otherwise:
		say "Shouldn't you have a bed first?".
		
[for getting water from water purifier in Homemade Bunker]
Getting water is an action applying to nothing.
Understand "get water" as getting water.
Report getting water:
	if the player is in Homemade Bunker:
		say "WIP. Will be able to receive item eventually.";
	otherwise:
		say "You should probably find a purifier first.".

[for defense activation in Control Room]
defensesActivated is a truth state that varies.
defensesActivated is initially false.
Activating is an action applying to nothing.
Understand "activate control panels" or "acp" as activating.
Report activating:
	if the player is in Control Room:
		if defensesActivated is false:
			now defensesActivated is true;
			say "Activating defenses.";
		otherwise:
			now defensesActivated is false;
			say "Deactivating defenses.";
	otherwise:
		say "You should probably be in the bunker then.".

[for getting food from the garden in the Garden Center]
Getting food is an action applying to nothing.
Understand "get food" as getting food.
Report getting food:
	if the player is in Garden Center:
		say "WIP. Will be able to receive item eventually.";
	otherwise:
		say "Shouldn't you find a food source first?".

[help generic]
Helping is an action applying to nothing.
Understand "help" as helping.
Report helping:
	say "Say help <number> for more information. [line break]1: Stat descriptions. [line break]2: Cheating".

[help section 1]
HelpingOne is an action applying to nothing.
Understand "help 1" and "help one" as HelpingOne.
Report HelpingOne:
	say "HP is your current health points. [line break]MaxHP is the maximum health you may get. [line break]Defense is how much defense you have. It also defends against radiation. [line break]Radiation is how much radiation has accumulated in your system. A high number is dangerous.[line break]Strength is a measure of physical force, how much brute force you have.[line break]Dexterity is a measure of skill, how well one can use their hands. [line break]Stamina is a measure of endurance, of toughness. More endurance affects HP and Strength. [line break]Charisma is how likable you are. May affect NPC conversations. [line break]Intelligence is how smart you are, whether you know something or not. [line break]Perception is how effectively you can use that intelligence. It is acts as inspiration or noticing other things.".

[help section 2]
HelpingTwo is an action applying to nothing.
Understand "help 2" and "help two" as HelpingTwo.
Report HelpingTwo:
	say "Available cheats: [line break]shuffle stats -> shuffle beginning stats, all stats are between 3 and 18.[line break]upgrade me -> +1 to the 6 stats.".


Section - Items----------------------------------------------------------------------------

The backpack is a thing.
The backpack is an closed container in Homemade Bunker.
The backpack is wearable.
The backpack is a player's holdall.
Understand "backp" and "bpack" and "bp" as the backpack.
The description of the backpack is "A rather nice military style backpack. It never seems to be too heavy, or full.".

The flashlight is a thing.
The flashlight is in the backpack.
The flashlight is an electric light.
Understand "flash" and "light" as the flashlight.
The description of flashlight is "A long black metal flashlight. Bright and rather durable. [if the flashlight is lit]It is currently on.[otherwise]It is currently off.".

Toxic water is a kind of thing.
The indefinite article of toxic water is "some".
The description of toxic water is "Water. Too poisonous to drink. Maybe some you can [bold type]purify[roman type] it with a water purifier?".
[(quantity) toxic water is in (location)]

Dirty water is a kind of thing.
The indefinite article of dirty water is "some".
The description of dirty water is "Water. Slightly dirty. Nothing a good [bold type]boil[roman type] will help.".
[(quantity) dirty water is in (location)]

Clean water is a kind of thing.
The indefinite article of clean water is "some".
The description of clean water is "Clean water. Good to drink.".
3 clean water is in storage.

Foraged food is a kind of thing.
The indefinite article of foraged food is "some".
The description of foraged food is "An assorted sundry collection of herbs, leaves, fruits and berries.".
[(quantity) foraged food is in (location)]

Raw meat is a kind of thing.
The indefinite article of raw meat is "some".
The description of raw meat is "Some raw meat. Would not be safe to eat. Try [bold type]cook[roman type]ing the meat.".
[(quantity) raw meat is in (location)]

Cooked meat is a kind of thing.
The indefinite article of cooked meat is "some".
The description of cooked meat is "Some cooked meat. Could be steak, pork chops, chicken, rabbit, venison, etc.".
3 cooked meat is in storage.

Section - Unexcusable Cheats---------------------------------------------------------------

Nonexistence is a room. 
The description of Nonexistence is "You don't exist! You shouldn't even be here. That aside, items will be here, and NPCs as well.".

cheater is a truth state that varies.
cheater is initially false.

shufflingStats is an action applying to nothing.
Understand "shuffle stats" as shufflingStats.
Report shufflingStats:
	now the strength of the player is (a random number from 1 to 6) + (a random number from 1 to 6) + (a random number from 1 to 6); 
	now the dexterity of the player is (a random number from 1 to 6) + (a random number from 1 to 6) + (a random number from 1 to 6);
	now the stamina of the player is (a random number from 1 to 6) + (a random number from 1 to 6) + (a random number from 1 to 6);
	now the charisma of the player is (a random number from 1 to 6) + (a random number from 1 to 6) + (a random number from 1 to 6);
	now the intelligence of the player is (a random number from 1 to 6) + (a random number from 1 to 6) + (a random number from 1 to 6);
	now the perception of the player is (a random number from 1 to 6) + (a random number from 1 to 6) + (a random number from 1 to 6);
	now cheater is true;
	say "Cheater! - stats reshuffled.".

upgrading is an action applying to nothing.
understand "upgrade me" as upgrading.
Report upgrading:
	now the strength of the player is the strength of the player + 1;
	now the dexterity of the player is the dexterity of the player + 1;
	now the stamina of the player is the stamina of the player + 1;
	now the charisma of the player is the charisma of the player + 1;
	now the intelligence of the player is the intelligence of the player + 1;
	now the perception of the player is the perception of the player + 1;
	now cheater is true;
	say "Cheater! - stats upgraded.".

Section - Tables of Reference Information--------------------------------------------------

Table of Status Bar
left	central	right 
"Name: [name of the player] | HP: [HP of the player]/[MaxHP of the player] | DEF: [defense of the player] | RAD: [radiation of the player]"	""	"[map region of the location]"
"STR: [strength of the player] | DEX: [dexterity of the player] | STA: [stamina of the player] | CHA: [charisma of the player] | INT: [intelligence of the player] | PER: [perception of the player]"	""	"[location of the player]"

Section - Notes ---------------------------------------------------------------------------

[D&D Rules
See "https://www.americanroads.us/DandD/DnD_Basic_Rules_Moldvay.pdf" for details

Dice Rules
type of dice - 4, 6, 8, 10, 12, 20
4d6 refers to 4 rolls of d6. Totals are added.
d% is percentage roll - roll d10 for the tens, d10 again for ones.
Ex: roll 7, roll 4 -> 74%

PC rules (player character)
Strength, Dexterity, Stamina, Charisma, Intelligence, Perception
Roll 3d6 for each ability
Pick special abilities
Name character

- Create a limited map of maybe 40 rooms max
- Make rooms appear/disappear on certain conditions
- Weapons, items, and armor can give buffs/debuffs
- Chaos token idea? or use dice to determine results
- Wandering enemies, multiple factions]

Section - Work Area -----------------------------------------------------------------------

Understand the commands "attack", "kill", "destroy", "break", "murder", "smash" as something new.
Understand the commands "crack", "punch", "fight", "thump", "hit", "torture", "wreck" as something new.

A weapon is a kind of thing.
A weapon can be singleH or doubleH. [singe hand or double hand]
A weapon can be ranged or melee.

[need to equip weapon for combat!]

RedefinedAttacking is an action applying to two things.
Understand "attack [something] with [weapon]" as RedefinedAttacking.

Report RedefinedAttacking:
	say "stabby stabby stab stab".
